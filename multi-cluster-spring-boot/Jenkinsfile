openshift.withCluster() {
  env.NAMESPACE = openshift.project()
  env.SKOPEO_IMAGE = openshift.selector( 'is/jenkins-slave-image-mgmt' ).object().status.dockerImageRepository
  env.APP_NAME = "${env.JOB_NAME}".replaceAll(/-?pipeline-?/, '').replaceAll(/-?${env.NAMESPACE}-?/, '')
  echo "Starting Pipeline for ${APP_NAME}..."
  def projectBase = "${env.NAMESPACE}".replaceAll(/-dev/, '')
  env.STAGE1 = "${projectBase}-dev"
  env.STAGE2 = "${projectBase}-stage"
  env.STAGE3 = "${projectBase}-prod"
}


pipeline {
  agent { label 'maven' }
  stages {
    stage('Code Build') {
      steps {
        git url: "${SOURCE_CODE_URL}"
        sh "mvn clean install"
      }
    }
    stage('Image Build') {
      steps {
        sh """
          rm -rf oc-build && mkdir -p oc-build/deployments
          for t in \$(echo "jar;war;ear" | tr ";" "\\n"); do
            cp -rfv ./target/*.\$t oc-build/deployments/ 2> /dev/null || echo "No \$t files"
          done
        """
        script {
          openshift.withCluster() {
            openshift.startBuild("${APP_NAME}", "--from-dir=oc-build", "--wait", "--follow")
          }
        }
      }
    }

    stage ('Verify Deployment to Dev') {
      steps {
        script {
          openshift.withCluster() {
            def dcObj = openshift.selector('dc', env.APP_NAME).object()
            def podSelector = openshift.selector('pod', [deployment: "${APP_NAME}-${dcObj.status.latestVersion}"])
            podSelector.untilEach {
                echo "pod: ${it.name()}"
                return it.object().status.containerStatuses[0].ready
            }
          }
        }
      }
    }

    stage('Promote to Stage') {
      steps {
        script {
          openshift.withCluster() {
            openshift.tag("${env.STAGE1}/${env.APP_NAME}:latest", "${env.STAGE2}/${env.APP_NAME}:latest")
          }
        }
      }
    }

    stage('Promote to Prod') {
      agent {
        kubernetes {
          label 'promotion-slave'
          cloud 'openshift'
          serviceAccount 'jenkins'
          containerTemplate {
            name 'jnlp'
            image "docker-registry.default.svc:5000/${NAMESPACE}/jenkins-slave-image-mgmt"
            alwaysPullImage true
            workingDir '/tmp'
            args '${computer.jnlpmac} ${computer.name}'
            ttyEnabled false
          }
        }
      }
      steps {
        script {
          openshift.withCluster() {

            def localToken = readFile('/var/run/secrets/kubernetes.io/serviceaccount/token').trim()

            def secretData = openshift.selector('secret/prod-credentials').object().data
            def registry = Base64.decodeBase64(secretData.registry)
            def token = Base64.decodeBase64(secretData.token)

            openshift.withProject("${STAGE2}") {
              def imageRegistry = openshift.selector( 'is', "${APP_NAME}").object().status.dockerImageRepository
              echo "Promoting ${imageRegistry}/${STAGE2}/${APP_NAME} -> ${registry}/${STAGE3}/${APP_NAME}"
              sh """
              skopeo copy --remove-signatures \
                --src-creds openshift:${localToken} --src-cert-dir /run/secrets/kubernetes.io/serviceaccount/ca.crt \
                --dest-creds openshift:${token}  --dest-tls-verify false \
                docker://${imageRegistry}/${STAGE2}/${APP_NAME} docker://${registry}/${STAGE3}/${APP_NAME}
              """
            }

          }
        }
      }
    }
  }
}
